apply plugin: 'base'
apply plugin: "com.github.node-gradle.node"

node {
    // Version of node to use.
    version = '10.21.0'

    // Version of npm to use.
    npmVersion = '6.14.4'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.buildDir}/npm")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}")
}


npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir 'build'
}

task packageNpmApp(type: Zip) {
    dependsOn npm_run_build
    baseName 'nes'
    extension 'jcui'
    destinationDir file("${projectDir}/../app/src/main/res/raw")
    from('build') {
    }
}

configurations {
    npmResources
}
configurations.default.extendsFrom(configurations.npmResources)


artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type "jcui"
    }
}

clean {
    delete packageNpmApp.archivePath
}

assemble.dependsOn packageNpmApp
